#!/bin/csh
# Assemble PostScript portrait pages "sixteen up" (4 x 4, 25% reduction).
# Arguments required: input filenames.
# Output to stdout.
# TJP 15-Jul-94 for A4
# STM 24-Aug-94 for 8.5x11
#----------------------------------------------------------------------
set SCALE  = 0.25
set HEIGHT = 10.0
set WIDTH  = 7.75
set HOFF   = 1.75
set VOFF   = 1.0
set NX     = 4
set NY     = 4
set DONE   = 0
@ IX = 0
@ IY = $NY - 1
cat <<EOF
%!
/begininclude{/level0 save def /showpage {} def} bind def
/endinclude{level0 restore} bind def
/inch{72 mul} bind def
EOF

while ($1 != "")
    echo
    echo "%%>>>" $IX $IY $1
    echo begininclude
    echo ${SCALE} ${SCALE} scale
    echo ${WIDTH} ${IX} mul ${HOFF} add inch ${HEIGHT} ${IY} mul ${VOFF} add inch translate
    /usr/local/bin/zcat $1; shift
    echo endinclude
    @ DONE = $DONE + 1
    @ IX = $IX + 1
    if ($IX == $NX) then
        @ IX = 0
        @ IY = $IY - 1
    endif
    if ($IY < 0 ) then
        echo
        echo showpage
        @ IY = $NY - 1
        set DONE = 0
    endif
end

if ($DONE > 0) then
   echo 
   echo showpage
endif
